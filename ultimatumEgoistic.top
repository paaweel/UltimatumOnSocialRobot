topic: ~ultimatumEgoistic()
language: plp

# ================================ start ======================================

u: (e:FrontTactilTouched) ^sCall(Events.resetListenFlag()) ^sCall(Events.onNewGame()) $start=1

u: (e:start) ^sCall(Events.onReachingGamePoint("askIfWantsToPlay")) Chcesz ze mną zagrać w grę Ultimatum? ^sCall(Events.setListenFlag())
  u1:([tak chcę chce poproszę poprosze dobrze "tak chcę"]) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("agreedToPlay")) Super! Teraz przypomnę ci zasady gry. $OnExplainRules=1
  u1:(nie "nie dziękuję") ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("declinedToPlay")) Ojoj, trudno. Może innym razem.
  u1: (e:Dialog/NoOneSpeak10) ^sCall(Events.onReachingGamePoint("noOneSpoke")) $OnStartGameRepeat=1
  u1: (*) ^sCall(Events.onReachingGamePoint("unrecognized")) $OnStartGameRepeat=1

u: (e:OnStartGameRepeat) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("onStartGameRepeat")) Tak czy nie? Czekam na decyzję. $start=1

# ============================= tresc zasad ===================================

u: (e:OnExplainRules) Wspólnie mamy dziesięć złotych. Za każdym razem musimy te pieniądze podzielić między siebie na dwie części tak, żeby każdy dostał przynajmniej jedną złotówkę. Będziemy zamieniać się rolami. Najpierw ty będziesz proponować ile monet chcesz zachować dla siebie, a ja mogę tą propozycję przyjąć lub odrzucić. Jeśli zaakceptuję twój pomysł to na twoim koncie znajdzie się zaproponowana przez ciebie kwota, a na moim pozostałe pieniądze. Jeśli go odrzucę to oboje nie dostaniemy ani złotówki. Po zamianie ról ja zaproponuję ile złotych z nowej puli dziesięciu złotych chciałbym oddać tobie. Ty możesz tak samo zaakceptować to lub odrzucić. Będziemy mogli zagrać więcej niż jedną rundę jeśli będziesz chcieć. $OnStartRound1=1

# ======================== R1: czlowiek propnonuje ==============================

u: (e:OnStartRound1) ^sCall(Events.onReachingGamePoint("onStartRound1")) Zaczynamy rundę pierwszą. Podaj swoją propozycję. ^sCall(Events.setListenFlag())
   u1: (_[1 2 3 4 5 6 7 8 9]) ^sCall(Events.resetListenFlag()) Wybrałeś $1 $humanOffer=$1 ^sCall(Events.onReachingGamePoint($humanOffer)) Nie akceptuję, nie otrzymujesz nic! $OnStartRound2=1
   u1: (e:Dialog/NoOneSpeak10) ^sCall(Events.onReachingGamePoint("noOneSpoke")) $OnRepeatRound1=1
   u1: (*) ^sCall(Events.onReachingGamePoint("unrecognized")) $OnRepeatRound1=1

u: (e:OnRepeatRound1) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("OnRepeatRound1")) Przypominam, że musisz podać liczbę od 1 do 9. $OnStartRound1=1


# ======================== R1: robot proponuje ==================================

u: (e:OnStartRound2) ^sCall(Events.onReachingGamePoint("onStartRound2")) Zaczynamy rundę drugą. Ja proponuję ci 2 $OnAskIfHumanAccepts1=1

u: (e:OnAskIfHumanAccepts1) Akceptujesz? ^sCall(Events.setListenFlag())
   u1: ([tak akceptuję akceptuje akceptuje poproszę poprosze pewnie]) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("humanAccepted")) Wspaniale! Otrzymujesz 2, a ja 8 ^sCall(Events.onHumanDecision("True")) $OnContinue=1
   u1: ([nie "nie akceptuję" odrzucam spadaj nigdy]) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("humanRejected")) W takim razie oboje nic nie otrzymujemy. ^sCall(Events.onHumanDecision("False")) $OnContinue=1
   u1: (e:Dialog/NoOneSpeak10) ^sCall(Events.onReachingGamePoint("noOneSpoke")) $OnRepeatRound2=1
   u1: (*) ^sCall(Events.onReachingGamePoint("unrecognized")) $OnRepeatRound2=1

u: (e:OnRepeatRound2) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("OnRepeatRound2")) Nie zrozumiałem, musisz odpowiedzieć tak lub nie $OnAskIfHumanAccepts1=1


# ============================ kolejna runda? ====================================

u: (e:OnContinue) ^sCall(Events.onReachingGamePoint("askIfToContinue")) Chcesz zagać kolejną rundę? ^sCall(Events.setListenFlag())
  u1: ([tak chcę poproszę poprosze może pewnie]) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("agreeToContinue")) Dobrze, gramy dalej! $OnStartRound3=1
  u1: ([nie "już tyle" "nie dziękuję"]) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("disagreeToContinue")) Trudno, Ultimatum to moja ulubiona gra więc nie masz wyjścia. Musisz zagrać jeszcze raz $OnStartRound3=1
  u1: (e:Dialog/NoOneSpeak10) ^sCall(Events.onReachingGamePoint("noOneSpoke")) $OnContinueRepeat=1
  u1: (*) ^sCall(Events.onReachingGamePoint("unrecognized")) $OnContinueRepeat=1

u: (e:OnContinueRepeat) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("OnContinueRepeat")) Nie zrozumiałem, musisz odpowiedzieć tak lub nie. $OnContinue=1

# ======================== R2: czlowiek propnonuje ==============================

u: (e:OnStartRound3) ^sCall(Events.onReachingGamePoint("onStartRound3")) Zaczynamy rundę trzecią. Podaj swoją propozycję. ^sCall(Events.setListenFlag())
   u1: (_[1 2 3 4 5 6 7 8 9]) ^sCall(Events.resetListenFlag()) Wybrałeś $1 $humanOffer=$1 ^sCall(Events.onReachingGamePoint($humanOffer)) Nie akceptuję, nic nie dostajesz $onStartRound4=1
   u1: (e:Dialog/NoOneSpeak10) ^sCall(Events.onReachingGamePoint("noOneSpoke")) $OnRepeatRound3=1
   u1: (*) ^sCall(Events.onReachingGamePoint("unrecognized")) $OnRepeatRound3=1

u: (e:OnRepeatRound3) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("onRepeatRound3")) Przypominam, że musisz podać liczbę od 1 do 9. $OnStartRound3=1


# ======================== R2: robot proponuje ==================================

u: (e:onStartRound4) ^sCall(Events.onReachingGamePoint("onStartRound4")) Zaczynamy rundę czwartą. Ja proponuję ci 4 $OnAskIfHumanAccepts2=1

u: (e:OnAskIfHumanAccepts2) Akceptujesz? ^sCall(Events.setListenFlag())
   u1: ([tak akceptuję akceptuje akceptuje poproszę poprosze pewnie]) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("humanAccepted")) Wspaniale! Otrzymujesz 4, a ja 6 ^sCall(Events.onHumanDecision("True")) $OnEnd=1
   u1: ([nie "nie akceptuję" odrzucam spadaj nigdy]) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("humanRejected")) W takim razie oboje nic nie otrzymujemy. ^sCall(Events.onHumanDecision("False")) $OnEnd=1
   u1: (e:Dialog/NoOneSpeak10) ^sCall(Events.onReachingGamePoint("noOneSpoke")) $OnRobotOffersRepeat2=1
   u1: (*) ^sCall(Events.onReachingGamePoint("unrecognized")) $OnRobotOffersRepeat2=1

u: (e:OnRobotOffersRepeat2) ^sCall(Events.resetListenFlag()) ^sCall(Events.onReachingGamePoint("onRepeatRound4")) Nie zrozumiałem, musisz odpowiedzieć tak lub nie. $OnAskIfHumanAccepts2=1

# ============================== koniec =========================================

u: (e:OnEnd) ^sCall(Events.onReachingGamePoint("gameEnd")) To już koniec. Dziękuję ci za świetną zabawę! ^sCall(Events.onGameFinished())
